plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'site.hanschen.glinspector'
            implementationClass = 'site.hanschen.glinspector.InspectorPlugin'
        }
    }
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'site.hanschen'
            artifactId = 'glinspector'
            version = '1.0.4'
            from components.java

            pom {
                name = 'GLInspector'
                url = 'https://github.com/hanschencoder/GLInspector'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = project.hasProperty('upload.user') ? project.property('upload.user') : System.getenv('UPLOAD.USER')
                password = project.hasProperty('upload.password') ? project.property('upload.password') : System.getenv('UPLOAD.PASSWORD')
            }
            url = "https://s01.oss.sonatype.org/content/repositories/releases"
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'com.android.tools.build:gradle:7.1.3'
    implementation 'org.ow2.asm:asm:9.1'
    implementation 'org.ow2.asm:asm-commons:9.1'
    implementation 'commons-io:commons-io:2.11.0'
}